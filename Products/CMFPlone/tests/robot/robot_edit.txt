*** Settings ***

Library  Selenium2Library  timeout=10  implicit_wait=0.5

Variables  plone/app/testing/interfaces.py

Suite Setup  Suite Setup
Suite Teardown  Suite Teardown


*** Variables ***

${PORTAL_URL}  http://localhost:55001/plone
${PORT} =  55001
${ZOPE_URL} =  http://localhost:${PORT}
${PLONE_URL} =  ${ZOPE_URL}/plone
${BROWSER} =  Firefox

${TITLE} =  An edited page
${PAGE_ID} =  an-edited-page

*** Test cases ***

Scenario: A page is opened to edit
    Given an edited page
     Then i have the title input field
      and i can only see the default tab

Scenario: Switch tabs
    Given an edited page
     When i click categorization tab
     Then the categorization tab is shown
      and no other tab is shown

Scenario: Adding a related item
    [Tags]  doit
    # Order of the next two lines is important
    # First we're creating a new item and then editing the original page
    Given at least one other item
      and an edited page
     When i click categorization tab
      and i click the add related item button
      and an overlay pops up
      and i select a related item
      and i close the overlay
      and i save the page
     Then the related item is shown in the page

*** Keywords ***

Suite Setup
    Open browser  ${PORTAL_URL}
    Log in  ${SITE_OWNER_NAME}  ${SITE_OWNER_PASSWORD}
    Go to  ${PORTAL_URL}/createObject?type_name=Document
    Input text  name=title  ${TITLE}
    Click Button  Save

Suite Teardown
    Close All Browsers

Log in
    [Arguments]  ${userid}  ${password}
    Go to  ${PORTAL_URL}/login_form
    Page should contain element  __ac_name
    Page should contain element  __ac_password
    Page should contain button  Log in
    Input text  __ac_name  ${userid}
    Input text  __ac_password  ${password}
    Click Button  Log in

an edited page
    Go to  ${PORTAL_URL}/${PAGE_ID}/edit

i have the title input field
    Element Should Be Visible  xpath=//fieldset[@id='fieldset-default']

i can only see the default tab
    Element Should Not Be Visible  xpath=//fieldset[@id='fieldset-settings']
    Element Should Not Be Visible  xpath=//fieldset[@id='fieldset-dates']
    Element Should Not Be Visible  xpath=//fieldset[@id='fieldset-categorization']

i click categorization tab
    Click link  xpath=//a[@id='fieldsetlegend-categorization']

the categorization tab is shown
    Element Should Be Visible  xpath=//fieldset[@id='fieldset-categorization']

no other tab is shown
    Element Should Not Be Visible  xpath=//fieldset[@id='fieldset-dates']
    Element Should Not Be Visible  xpath=//fieldset[@id='fieldset-default']
    Element Should Not Be Visible  xpath=//fieldset[@id='fieldset-settings']

at least one other item
    Go to  ${PORTAL_URL}/createObject?type_name=Document
    Input text  name=title  ${TITLE}
    Click Button  Save

i click the add related item button
    Click Button  xpath=//input[contains(@class, 'addreference')]

an overlay pops up
    Wait Until Page Contains Element  xpath=//div[contains(@class, 'overlay')]//input[@class='insertreference']

i select a related item
    Click Element  xpath=//div[contains(@class, 'overlay')]//input[@class='insertreference']
    Wait until keyword succeeds  10s  1s  Xpath Should Match X Times  //ul[@id = 'ref_browser_items_relatedItems']/descendant::input  1

i close the overlay
   Click Element  xpath=//div[contains(@class, 'overlay')]//div[@class='close']

i save the page
   Click Button  Save

the related item is shown in the page
   Xpath Should Match X Times  //dl[@id = 'relatedItemBox']/dd  1
